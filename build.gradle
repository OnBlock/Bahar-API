plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
}

group 'com.baharmc'
version '0.1+build.1'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

configurations {
    fatJar
    compile.extendsFrom fatJar
}

dependencies {
    compile 'org.jetbrains:annotations:17.0.0'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allJava
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath
}

publishing {
    publications {
        BaharAPIPub(MavenPublication) {
            from components.java
            groupId 'com.baharmc'
            artifactId 'BaharAPI'
            version version

            artifact sourceJar
            artifact packageJavadoc
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['BaharAPIPub']
    pkg {
        repo = 'maven'
        name = 'Bahar-API'
        userOrg = 'baharmc'
        websiteUrl = 'baharmc.com'
        desc = 'Plugin API of the Bahar servers'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/BaharMC/Bahar-API.git'
        githubRepo = 'https://github.com/BaharMC/Bahar-API'
        publicDownloadNumbers = true
    }
}